; Script generated by the Inno Script Studio Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define AppName "ToolChain"
#define InstallerOutputFolder = "RS-MET_-_ToolChain"
#define AppVersion "1.0.0"
#define AppPublisher "RS-MET"
#define AppURL "http://www.rs-met.com/"

[Setup]
AppName={#AppName}
AppVersion={#AppVersion}
AppPublisher={#AppPublisher}
AppPublisherURL={#AppURL}
AppSupportURL={#AppURL}
AppUpdatesURL={#AppURL}
DefaultDirName={userappdata}\{#AppPublisher}
DefaultGroupName={#AppName}
AllowNoIcons=yes
OutputDir={#InstallerOutputFolder}
OutputBaseFilename={#AppName} Installer
Compression=lzma
SolidCompression=yes
ArchitecturesInstallIn64BitMode=x64
InfoBeforeFile=InstallationInstructions.txt

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Types]
Name: "custom"; Description: "Custom installation"; Flags: iscustom

[Files]
Source: "RS-MET\*"; DestDir: "{userappdata}\{#AppPublisher}"; Flags: ignoreversion recursesubdirs createallsubdirs promptifolder; Components: Presets
Source: "{#AppName}_VST_x64.dll"; DestDir: "{code:GetVST2Dir_64}"; Flags: ignoreversion 64bit; Components: VST2_64;

[Components]
Name: "Presets"; Description: "Check this to install Samples and Presets -> WARNING: This will overwrite all factory presets, have you saved custom presets over the factory presets?"; Types: custom; Flags: checkablealone
Name: "VST2_64"; Description: "64-bit VST2 Plugin (.dll)"; Types: custom; Flags: checkablealone

[Code]
var
  OkToCopyLog : Boolean;
  PresetsDirPage: TInputDirWizardPage;
  VST2DirPage_64: TInputDirWizardPage;

procedure InitializeWizard;
begin
  VST2DirPage_64 := CreateInputDirPage(wpSelectDir, 
  'Confirm {#AppName} 64-Bit VST2 Plugin Directory', '',
  'Select the 64-bit VST2 Plugin folder.',
  False, '');
  VST2DirPage_64.Add('');
  VST2DirPage_64.Values[0] := ExpandConstant('{pf}\VstPlugins\');
end;

function GetVST2Dir_64(Param: String): String;
begin
  Result := VST2DirPage_64.Values[0]
end;